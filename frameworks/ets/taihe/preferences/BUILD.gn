# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/distributeddatamgr/preferences/preferences.gni")

subsystem_name = "distributeddatamgr"
part_name = "preferences"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

copy_taihe_idl("copy_taihe") {
  sources = [
    "idl/ohos.data.preferences.taihe",
  ]
  external_deps = []
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.data.preferences.ani.cpp",
    "$taihe_generated_file_path/src/ohos.data.preferences.abi.c",
  ]
}

taihe_shared_library("preferences_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
    integer_overflow = true
    ubsan = true
  }
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"
  include_dirs = [
    "include",
    "${preferences_base_path}/frameworks/common/include/",
    "${preferences_base_path}/frameworks/ets/taihe/common/include/",
  ]
  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.data.preferences.impl.cpp",
    "src/preferences_proxy.cpp",
    "${preferences_base_path}/frameworks/ets/taihe/common/src/ani_ability.cpp",
    "${preferences_base_path}/frameworks/ets/taihe/common/src/ani_common_utils.cpp",
    "${preferences_base_path}/frameworks/ets/taihe/common/src/taihe_preferences_error.cpp",
    "${preferences_base_path}/frameworks/ets/taihe/common/src/taihe_common_utils.cpp",
    "${preferences_base_path}/frameworks/ets/taihe/common/src/taihe_preferences_observer.cpp",
  ]
  deps = [
    ":run_taihe",
    "${preferences_base_path}/interfaces/inner_api:native_preferences"
  ]
  external_deps = [
    "ability_runtime:abilitykit_native",
    "ability_runtime:ani_base_context",
    "bounds_checking_function:libsec_shared",
    "hilog:libhilog"
  ]
}

generate_static_abc("preferences_abc") {
  base_url = "$taihe_generated_file_path"
  files = [
    "$taihe_generated_file_path/@ohos.data.preferences.ets",
  ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/preferences_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("preferences_etc") {
  source = "$target_out_dir/preferences_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":preferences_abc" ]
}

group("preferences_taihe") {
  deps = [
    ":preferences_etc",
    ":preferences_taihe_native",
  ]
}